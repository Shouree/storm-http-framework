use ui;
use graphics;
use core:geometry;
use lang:bs:macro;


/**
 * A data structure instantiated somewhere. Managed by a Workspace.
 */
class Data extends Drawable {
	// Draw this data item at its current location.
	void draw(Graphics g) : abstract {
		Rect r = rect;
		g.fill(r, dataBg);
		g.draw(r, dataFg);
	}

	void drawLinks(Graphics g, Bool active) : override {}

	// Update the contents of the visualization from the actual data.
	void update(Workspace work, unsafe:RawPtr object, Nat offset) : abstract;
}

/**
 * Data element with a caption.
 */
class DataWithCaption extends Data {
	init(Data wrap, Str caption) {
		Text c(caption, captionFont);

		init() {
			wrap = wrap;
			caption = c;
			captionSz = c.size + compositeBorder*2;
		}
	}

	Size size() : override {
		Size sz = wrap.size();
		sz.w = max(sz.w, captionSz.w);
		sz.h += captionSz.h;
		sz;
	}

	void draw(Graphics g) : override {
		Size cSz = captionSz;
		cSz.w = max(cSz.w, wrap.size.w);
		g.fill(Rect(pos, cSz), captionBg);
		g.draw(caption, captionFg, pos + compositeBorder);
		g.draw(Rect(pos, cSz), captionFg);

		wrap.pos = pos + Size(0, captionSz.h);
		wrap.draw(g);
	}

	void drawLinks(Graphics g, Bool active) : override {
		wrap.drawLinks(g, active);
	}

	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {
		wrap.update(work, object, offset);
	}

	// The drawable we're wrapping.
	private Data wrap;

	// Caption text.
	private Text caption;

	// Size of the caption area.
	private Size captionSz;
}

/**
 * Unknown data element.
 */
class UnknownData extends Data {
	private Text text;

	init() {
		init() {
			text = Text("?", dataFont);
		}

		size = text.size + dataBorder*2;
	}

	void draw(Graphics g) : override {
		super:draw(g);
		g.draw(text, dataFg, pos + dataBorder);
	}

	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {}
}


/**
 * A primitive datatype, such as an integer. Represented as text.
 */
class Primitive extends Data {
	// The text to draw.
	private Text text;

	// Type of the data we're displaying.
	core:lang:Type type;

	init(core:lang:Type type, Str initial) {
		init() {
			text = Text(initial, dataFont);
			type = type;
		}
	}

	void draw(Graphics g) : override {
		super:draw(g);
		g.draw(text, dataFg, pos + dataBorder);
	}

	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {
		if (s = convertPrimitive(type, object, offset)) {
			text = Text(s, dataFont);
			invalidateSize();
		}
	}

	Size computeSize() {
		text.size + dataBorder*2;
	}
}

/**
 * A pointer type, referring to some other data.
 */
class Pointer extends Data {
	protected Data? to;

	init() {
		init() {}

		size = Text("????", dataFont).size + dataBorder*2;
	}

	void draw(Graphics g) : override {
		super:draw(g);
		if (to) {
		} else {
			// Null.
			Rect r = rect;
			g.line(rect.p0, rect.p1, activeLink);
		}
	}

	void drawLinks(Graphics g, Bool active) : override {
		if (to) {
			Brush color = if (active) { activeLink; } else { inactiveLink; };
			Rect r = rect;
			g.line(r.center, to.pos, color);
		}
	}

	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {
		var ptr = object.readPtr(offset);
		if (ptr.any) {
			to = work.traverse(ptr);
		} else {
			to = null;
		}
	}
}

/**
 * A pointer on the stack. Examines the pointer passed directly to the object rather than
 * dereferencing it once first.
 */
class StackPointer extends Pointer {
	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {
		if (object.any) {
			to = work.traverse(object);
		} else {
			to = null;
		}
	}
}

/**
 * A composite type, consisting of one or more variables of other types.
 */
class Composite extends Data {
	init() {
		init() {}
	}

	void add(Str name, Nat offset, Data data, Bool divider) {
		if (content.empty)
			divider = false;

		content << Var(name, data, offset, divider);
		invalidateSize();
	}

	void draw(Graphics g) : override {
		super:draw(g);
		Float width = rect.size.w;
		Point at = pos + compositeBorder;

		for (v in content) {
			var nameSz = v.text.size;
			var dataSz = v.value.size;

			if (v.line) {
				g.line(Point(pos.x, at.y), Point(pos.x + width, at.y), dataFg);
				at.y += dataBorder.h;
			}

			// Align the text a bit nicer on the y-axis, to line up the baselines...
			g.draw(v.text, dataFg, at + Point(0, dataBorder.h));

			v.value.pos = at + Point(namesWidth + dataWidth - dataSz.w, 0);
			v.value.draw(g);

			at.y += max(nameSz.h, dataSz.h) + dataBorder.h;
		}
	}

	void drawLinks(Graphics g, Bool active) : override {
		for (v in content) {
			v.value.drawLinks(g, active);
		}
	}

	void update(Workspace work, unsafe:RawPtr object, Nat offset) : override {
		for (v in content) {
			v.value.update(work, object, offset + v.offset);
		}

		invalidateSize();
	}

private:
	class Var on Render {
		Str name;
		Text text;
		Data value;
		Nat offset;
		// Line before?
		Bool line;

		init(Str name, Data value, Nat offset, Bool divider) {
			init() {
				name = name;
				text = Text(name, dataFont);
				value = value;
				offset = offset;
				line = divider;
			}
		}
	}

	Var[] content;
	Float namesWidth;
	Float dataWidth;
	Float height;

	Size computeSize() : override {
		// TODO: It would probably be nice to recurse into
		// the values that are themselves variables and render them
		// more compactly.
		namesWidth = 0;
		dataWidth = 0;
		height = 0;
		for (v in content) {
			var nameSz = v.text.size;
			var dataSz = v.value.size;

			namesWidth = max(namesWidth, nameSz.w + dataBorder.w);
			dataWidth = max(dataWidth, dataSz.w);
			height += max(nameSz.h, dataSz.h) + dataBorder.h;

			if (v.line)
				height += dataBorder.h;
		}

		height -= dataBorder.h;
		Size(namesWidth + dataWidth, height) + compositeBorder*2;
	}
}
