use ui;
use core:geometry;
use core:io;
use progvis:net;
use graphics;
use lang:bs:macro;

dialog ProblemsDlg {
	public Action? action;
	private Client client;
	private progvis:Settings settings;

	layout Grid {
		expandCol: 0;
		expandRow: 0;
		ListView list(["Problem", "Author", "Status"]) {}
	}

	init(Client client, progvis:Settings settings) {
		init("Problems", Size(600, 500)) {
			client = client;
			settings = settings;
		}

		list.onSelect = &this.onListSelect;
		list.onActivate = &this.onListActivate;

		(spawn populateList()).detach();
	}

	private void populateList() {
		unless (response = client.query(ProblemListRequest()) as ProblemListResponse) {
			showMessage(this, "Connection error", "Failed to retrieve list of problems.");
			close(-1);
			return;
		}

		for (row in response.data) {
			Str status = if (row.attempted) { "Attempted"; } else { "New"; };
			list.add([row.title, row.author, status]);
		}
	}

	private void onListSelect(Nat id, Bool selected) {
		print("SELECT ${id}, ${selected}");
	}

	private void onListActivate(Nat id) {
		print("ACTIVATE ${id}");
	}
}
