use ui;
use progvis:net;

on Ui:

/**
 * Data for the entire Problem dialog. Allows asking for different types of problems.
 */
class ProblemData {
	private Str[] names;
	private View[] views;

	init(Client client) {
		init {}
	}

	void add(Str name, View view) {
		names << name;
		views << view;
	}

	Nat count() { names.count; }
	Str name(Nat id) { names[id]; }
	View view(Nat id) { views[id]; }
	Str[] allNames() { names; }
}

/**
 * Data for some view. Controls what kind of information shall be displayed.
 *
 * Does not actually store any data, but encapsulates the queries that need to be done to acquire the data.
 */
class View {
	protected Client client;

	init(Client client) {
		init { client = client; }
	}

	value Row {
		// Some ID used to refer to this row.
		Int id;

		// Data shown to the user.
		Str[] data;

		init(Int id, Str[] data) {
			init { id = id; data = data; }
		}
	}

	Str[] labels() : abstract;
	Row[] rows() : abstract;

	protected Str[] problemLabels() {
		["Problem name", "Author", "Solved"];
	}

	protected Row[] getProblems(Request req) {
		if (ans = client.query(req) as ProblemResponse) {
			Row[] data;
			for (r in ans.problems) {
				data << Row(r.id, [r.title, r.author, r.solutions.toS]);
			}
			return data;
		} else {
			return [];
		}
	}

	// Find an appropriate panel for when a row has been clicked.
	Panel details(Int id) : abstract;
}

/**
 * View for problems that we can solve (e.g. unsolved problems).
 */
class UnsolvedView extends View {
	init(Client client) {
		init(client) {}
	}

	Str[] labels() : override {
		problemLabels();
	}

	Row[] rows() : override {
		getProblems(ChallengesRequest(false));
	}

	Panel details(Int id) : override {
		Problem? parent;
		if (response = client.query(StatsRequest(id, false)) as StatsResponse) {
			parent = response.parent;
		}

		StatsPanel(parent, true);
	}
}

/**
 * View for problems that we have solved.
 */
class SolvedView extends View {
	init(Client client) {
		init(client) {}
	}

	Str[] labels() : override {
		problemLabels();
	}

	Row[] rows() : override {
		getProblems(ChallengesRequest(true));
	}

	Panel details(Int id) : override {
		if (response = client.query(StatsRequest(id, true)) as StatsResponse) {
			MultiPanel root;
			root.add(StatsPanel(response.parent, false));
			root.add(TablePanel(SolutionView(client, response.solutions)));
			root;
		} else {
			StatsPanel(null, false);
		}
	}
}

/**
 * View for own problems.
 */
class OwnView extends SolvedView {
	init(Client client) {
		init(client) {}
	}

	Row[] rows() : override {
		getProblems(OwnProblemsRequest());
	}
}

/**
 * View for solutions.
 */
class SolutionView extends View {
	private Solution[] solutions;

	init(Client client, Solution[] solutions) {
		init(client) { solutions = solutions; }
	}

	Str[] labels() : override {
		["Solution", "Author"];
	}

	Row[] rows() : override {
		Row[] result;
		for (r in solutions) {
			Str type = if (r.solution.empty) {
				"Looks good to me!";
			} else if (r.improved.empty) {
				"I found a problem...";
			} else {
				"I tried to fix the problem I found";
			};
			result << Row(r.id, [type, r.author]);
		}
		result;
	}

	Panel details(Int id) : override {
		// TODO: What to show here?
		// We should have "show problem" (if available), "show improvement" (if available).
		for (s in solutions) {
			if (s.id == id)
				return SolutionPanel(s);
		}

		// Good for "zero height".
		return MultiPanel();
	}
}
