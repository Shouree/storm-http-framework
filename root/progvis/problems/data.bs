use ui;
use progvis:net;

on Ui:

/**
 * Data for the entire Problem dialog. Allows asking for different types of problems.
 */
class ProblemData {
	private Str[] names;
	private View[] views;

	init() {
		init {}
	}

	void add(Str name, View view) {
		names << name;
		views << view;
	}

	Nat count() { names.count; }
	Str name(Nat id) { names[id]; }
	View view(Nat id) { views[id]; }
	Str[] allNames() { names; }
}

/**
 * Data for some view. Controls what kind of information shall be displayed.
 *
 * Does not actually store any data, but encapsulates the queries that need to be done to acquire the data.
 */
class View {
	protected Client client;

	init(Client client) {
		init { client = client; }
	}

	value Row {
		// Some ID used to refer to this row.
		Int id;

		// Data shown to the user.
		Str[] data;

		init(Int id, Str[] data) {
			init { id = id; data = data; }
		}
	}

	Str[] labels() : abstract;
	Row[] rows() : abstract;

	protected Str[] problemLabels() {
		["ID", "Problem name", "Author", "Type", "Improved"];
	}

	protected Row[] getProblems(Request req) {
		if (ans = client.query(req) as ProblemResponse) {
			ans.problems.sort();
			Row[] data;
			for (r in ans.problems) {
				data << Row(r.id, [r.id.toS, r.title, r.author, r.typeStr, r.solutions.toS]);
			}
			return data;
		} else {
			return [];
		}
	}

	// Find an appropriate panel for when a row has been clicked.
	Panel details(Int id) : abstract;
}

/**
 * View for problems that we can solve (e.g. unsolved problems).
 */
class UnsolvedView extends View {
	private Callback cb;
	private Bool currentError;

	init(Client client, Callback cb, Bool currentError) {
		init(client) {
			cb = cb;
			currentError = currentError;
		}
	}

	Str[] labels() : override {
		problemLabels();
	}

	Row[] rows() : override {
		getProblems(UnsolvedRequest(currentError));
	}

	Panel details(Int id) : override {
		ProblemInfo? parent;
		if (response = client.query(StatsRequest(id, false, false)) as StatsResponse) {
			parent = response.parent;
		}

		StatsPanel(id, parent, true, cb);
	}
}

/**
 * View for problems that we have solved.
 */
class SolvedView extends View {
	private Callback cb;

	init(Client client, Callback cb) {
		init(client) {
			cb = cb;
		}
	}

	Str[] labels() : override {
		problemLabels();
	}

	Row[] rows() : override {
		getProblems(SolvedProblemsRequest());
	}

	Panel details(Int id) : override {
		if (response = client.query(StatsRequest(id, true, true)) as StatsResponse) {
			MultiPanel root;
			root.add(StatsPanel(id, response.parent, false, cb));
			root.add(TablePanel(SolutionView(client, response, cb)));
			root;
		} else {
			StatsPanel(-1, null, false, cb);
		}
	}
}

/**
 * View for own problems.
 */
class OwnView extends SolvedView {
	init(Client client, Callback cb) {
		init(client, cb) {}
	}

	Row[] rows() : override {
		getProblems(OwnProblemsRequest());
	}
}


/**
 * View for solutions.
 */
class SolutionView extends View {
	private StatsResponse response;
	private Callback cb;

	init(Client client, StatsResponse response, Callback cb) {
		init(client) { response = response; cb = cb; }
	}

	Str[] labels() : override {
		["Author", "Solution"];
	}

	Row[] rows() : override {
		Row[] result;
		for (r in response.solutions) {
			result << Row(-r.id, [r.author, "Steps for error: ${r.type}"]);
		}
		for (r in response.improvements) {
			result << Row(r.id, [r.author, "Improved problem (${r.id}: ${r.title})"]);
		}
		result;
	}

	Panel details(Int id) : override {
		if (id < 0) {
			// This is a solution. Show the solution.
			for (x in response.solutions) {
				if (x.id == -id) {
					return SolutionPanel(x, cb);
				}
			}
		} else {
			// TODO: Check if we have solved this problem already. We should get that info in the stats response.
			ProblemInfo? parent;
			if (response = client.query(StatsRequest(id, false, false)) as StatsResponse) {
				parent = response.parent;
			}

			return StatsPanel(id, parent, false, cb);
		}

		// Good for "zero height".
		return MultiPanel();
	}
}
