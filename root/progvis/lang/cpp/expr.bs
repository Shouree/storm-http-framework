use core:lang;
use core:asm;
use lang:bs:macro;

on Compiler:

/**
 * Class representing an expression in C++.
 */
class Expr extends Stmt {
	init(SrcPos pos) {
		init(pos) {}
	}

	// Adapter from the statement.
	Bool code(CodeGen gen) : final {
		var r = result();
		code(gen, CodeResult());
		!r.nothing();
	}

	// Compute the result.
	ExprResult result() : abstract;

	// Does this expression represent a temporary value? I.e. is this an r-value?
	Bool temporary() {
		true;
	}

	// Generate code. 'desired' is the desired return type.
	void code(CodeGen gen, CodeResult res) : abstract;
}

/**
 * Integer literals.
 */
class IntLiteral extends Expr {
	init(SrcPos pos, Long val) {
		init(pos) { val = val; }
	}

	init(SStr str) {
		init(str.pos) { val = str.v.toLong(); }
	}

	private Long val;

	ExprResult result() : override {
		Value(named{Int}, true);
	}

	void code(CodeGen gen, CodeResult res) : override {
		if (!res.needed())
			return;

		var to = res.location(gen);
		if (t = res.type.type) {
			CppVar var = allocType(gen, t);
			var.adjust(gen, ptrA);
			if (t is named{Int}) {
				gen.l << mov(intRel(ptrA, Offset()), intConst(val.int));
			} else if (t is named{Long}) {
				gen.l << mov(longRel(ptrA, Offset()), longConst(val));
			} else {
				throw SyntaxError(pos, "Unknown type!");
			}
			gen.l << mov(to, ptrA);
			var.created(gen);
		}
		res.created(gen);
	}
}

/**
 * Node representing a yet unresolved identifier.
 *
 * If used on its own, it will try to act as a variable access.
 */
class NameExpr extends Expr {
	Scope scope;
	SimpleName name;
	private Expr? resolved;

	init(SrcPos pos, Scope scope, SimpleName name) {
		init(pos) { scope = scope; name = name; }
	}

	void code(CodeGen gen, CodeResult result) : override {
		resolve.code(gen, result);
	}

	ExprResult result() : override {
		resolve.result();
	}

	Bool temporary() : override {
		resolve.temporary();
	}

	private Expr resolve() {
		if (resolved)
			return resolved;

		Expr r = doResolve();
		resolved = r;
		r;
	}

	private Expr doResolve() {
		Named? found = scope.find(name);
		if (found as LocalVar) {
			LocalVarAccess r(pos, found);
			resolved = r;
			return r;
		}

		throw SyntaxError(pos, "Unable to resolve the name ${name} in this context!");
	}
}

// Try to "convert" a name expression into a function call.
Expr fnCall(Expr lhs, Expr[] actuals) {
	unless (lhs as NameExpr) {
		// TODO: We should try to call the function call operator on the object.
		throw SyntaxError(lhs.pos, "This is not something we can call.");
	}

	SimpleName name = lhs.name.clone;
	name.last.params = fnValues(actuals);
	Named? found = lhs.scope.find(name);
	if (found as Function)
		return FnCall(lhs.pos, found, actuals);

	throw SyntaxError(lhs.pos, "Found ${found}, which is not a function.");
}

// Compute the types of an array of values.
Value[] fnValues(Expr[] actuals) {
	Value[] r;
	for (x in actuals)
		r << x.result.type;
	r;
}

/**
 * Function calls.
 */
class FnCall extends Expr {
	private Function fn;
	private Expr[] actuals;

	init(SrcPos pos, Function fn, Expr[] actuals) {
		init(pos) {
			fn = fn;
			actuals = actuals;
		}
	}

	ExprResult result() : override {
		// We make sure to return references.
		fn.result.asRef();
	}

	void code(CodeGen gen, CodeResult res) : override {
		Operand[] params;
		for (i, x in actuals) {
			params << paramCode(gen, x, fn.params[i]);
		}

		if (t = fn.result.type) {
			if (res.type.ref & !fn.result.ref) {
				// Create a variable to store the result in.
				CppVar var = allocType(gen, t);
				var ref = res.safeLocation(gen, res.type);
				var.adjust(gen, ref);

				fn.autoCallRef(gen, params, ref);
				var.created(gen);
				res.created(gen);
			} else {
				fn.autoCall(gen, params, res);
			}
		} else {
			// Void result.
			fn.autoCall(gen, params, res);
		}
	}

	// Generate code for when someone else has already created the storage for us.
	void codeRef(CodeGen gen, CppVar var) {
		Operand[] params;
		for (i, x in actuals) {
			params << paramCode(gen, x, fn.params[i]);
		}

		if (t = fn.result.type) {
			if (fn.result.ref) {
				Var v = gen.l.createVar(gen.block, sPtr);
				var.adjust(gen, v);

				fn.autoCallRef(gen, params, v);
				var.created(gen);
			} else if (copyCtor = t.copyCtor) {
				// Need to copy!
				CodeResult r(fn.result, gen.block);
				fn.autoCall(gen, params, r);

				var.adjust(gen, ptrB);

				gen.l << lea(ptrA, r.location(gen));
				gen.l << fnParam(ptrDesc, ptrB);
				gen.l << fnParam(ptrDesc, ptrA);
				gen.l << fnCall(copyCtor.ref, true);
				r.created(gen);
				var.created(gen);
			} else {
				throw SyntaxError(pos, "Unable to copy the result.");
			}
		} else {
			// Void result.
			fn.autoCall(gen, params, CodeResult());
		}
	}
}

// Generate code for acquiring one parameter.
private Operand paramCode(CodeGen gen, Expr actual, Value formal) on Compiler {
	// TODO: Auto-casting?
	Value result = actual.result.type;
	if (!formal.canStore(result))
		throw SyntaxError(actual.pos, "Cannot convert to ${formal}.");

	if (formal.ref & !result.ref) {
		// Create a temporary variable and make a reference to it.
		result = formal.asRef(false);
		VarInfo tmpV = gen.createVar(result);
		CodeResult gr(result, tmpV);
		actual.code(gen, gr);

		VarInfo tmpRef = gen.createVar(formal);
		gen.l << lea(tmpRef.v, ptrRel(tmpV.v, Offset()));
		tmpRef.created(gen);
		tmpRef.v;
	} else if (!formal.ref & result.ref) {
		// We need to make a copy.
		if (!formal.isAsmType())
			throw SyntaxError(actual.pos, "Unable to manipulate this parameter!");

		CodeResult gr(formal.asRef(), gen.block);
		actual.code(gen, gr);

		Var v = gen.l.createVar(gen.block, formal.size());
		gen.l << mov(ptrA, gr.location(gen));
		gen.l << mov(v, xRel(v.size(), ptrA, Offset()));
		v;
	} else {
		CodeResult gr(formal, gen.block);
		actual.code(gen, gr);
		gr.location(gen);
	}
}

// Create an operator.
Expr operator(Block block, Expr lhs, SStr op, Expr rhs) on Compiler {
	var lType = lhs.result.type;
	var rType = rhs.result.type;

	SimplePart opName(op.v, [lType, rType]);
	if (t = lType.type) {
		if (fn = t.find(opName, block.scope) as Function)
			return FnCall(op.pos, fn, [lhs, rhs]);
	}

	if (fn = block.scope.find(SimpleName(opName)) as Function) {
		return FnCall(op.pos, fn, [lhs, rhs]);
	}

	throw SyntaxError(op.pos, "Failed to find operator ${op.v} for ${lType} and ${rType}.");
}

Expr operator(Block block, SStr op, Expr rhs) on Compiler {
	var rType = rhs.result.type;

	SimplePart opName(op.v + "*", [rType]);
	if (t = rType.type) {
		if (fn = t.find(opName, block.scope) as Function)
			return FnCall(op.pos, fn, [rhs]);
	}

	if (fn = block.scope.find(SimpleName(opName)) as Function) {
		return FnCall(op.pos, fn, [rhs]);
	}

	throw SyntaxError(op.pos, "Failed to find prefix operator ${op.v} for ${rType}.");
}
Expr operator(Block block, Expr lhs, SStr op) on Compiler {
	var lType = lhs.result.type;

	SimplePart opName("*" + op.v, [lType]);
	if (t = lType.type) {
		if (fn = t.find(opName, block.scope) as Function)
			return FnCall(op.pos, fn, [lhs]);
	}

	if (fn = block.scope.find(SimpleName(opName)) as Function) {
		return FnCall(op.pos, fn, [lhs]);
	}

	throw SyntaxError(op.pos, "Failed to find postfix operator ${op.v} for ${lType}.");
}

Expr dotOperator(Block block, Expr lhs, SStr rhs, Bool ptrLhs) {
	IntLiteral(rhs.pos, 0);
}

// Short-circuting operators.
Expr scAnd(Block block, Expr lhs, SStr op, Expr rhs) on Compiler { IntLiteral(op.pos, 0); }
Expr scOr(Block block, Expr lhs, SStr op, Expr rhs) on Compiler { IntLiteral(op.pos, 0); }
