use ui;
use graphics;
use core:geometry;
use core:lang;
use lang:bs:macro;

/**
 * Class representing the visual state of a thread.
 */
class ThreadData extends Drawable {
	// Thread that contains the source data.
	ProgThread thread;

	// Stack frames.
	FrameVal[] frames;

	// Source code view.
	SourceView source;

	// Create.
	init(ProgThread thread) {
		init {
			thread = thread;
			source(thread.owner);
		}
	}

	// Draw the thread.
	void draw(Graphics g, Bool active) : override {
		g.fill(rect, dataBg);
		g.draw(rect, dataFg);

		Point offset = rect.p0 + compositeBorder;
		Size minSize(0, rect.size.h - compositeBorder.h*3 - sourceSize.h);
		for (frame in frames) {
			Size sz = frame.size;
			minSize.w = max(minSize.w, sz.w);

			frame.pos = Rect(offset, minSize);

			Float delta = frame.captionHeight + compositeBorder.h;
			offset.x += stackOffset;
			offset.y += delta;
			minSize.h -= delta + compositeBorder.h;
		}

		Rect sourcePos(rect.p0, rect.p1 - compositeBorder);
		sourcePos.p0.x = rect.p0.x + compositeBorder.w;
		sourcePos.p0.y = sourcePos.p1.y - sourceSize.h;
		source.draw(g, sourcePos);
	}

	// Draw all links and all stack contents to make it appear on top of everything else.
	void drawLinks(Graphics g, Bool active) : override {
		for (id, frame in frames) {
			Bool a = id == frames.count - 1;
			frame.draw(g, a);
			frame.drawLinks(g, a & active);
		}
	}

	// Update our contents.
	void traverse(Workspace ws) {
		for (k, v in thread.frames) {
			if (k >= frames.count)
				frames.push(FrameVal(v));
			// Different frame?
			if (frames[k].source !is v)
				frames[k] = FrameVal(v);
			frames[k].traverse(ws);
		}

		while (frames.count > thread.frames.count)
			frames.pop();

		// Update the source view.
		if (pos = thread.pos)
			source.display(pos);

		invalidateSize();
	}

	protected Size computeSize() : override {
		Float w;
		Float h;

		for (Nat i = frames.count; i > 0; i--) {
			var frame = frames[i - 1];

			h += frame.captionHeight + compositeBorder.h * 2;

			Size sz = frame.size;
			h = max(h, sz.h);
			w = max(w, sz.w + i.int.float*stackOffset);
		}

		w = max(threadMinWidth, w);
		w = max(sourceSize.w, w);
		h += compositeBorder.h + sourceSize.h;

		Size(w, h) + compositeBorder*2;
	}
}


/**
 * A single stack frame in a thread.
 */
class FrameVal on Render {
	// Caption text.
	Text caption;

	// The stack frame we represent.
	StackFrame source;

	// Position.
	Rect pos;

	init(StackFrame frame) {
		init {
			caption = Text(frame.function, captionFont);
			source = frame;
		}
	}

	// Update our contents.
	void traverse(Workspace ws) {
		for (k, v in source.variables) {
			if (k >= vars.count) {
				vars.push(Var(ws, v));
			} else {
				vars[k].update(ws, v);
			}
		}

		while (vars.count > source.variables.count)
			vars.pop();

		updateSize();
	}

	// Get the height of the caption.
	Float captionHeight() {
		caption.size.h + compositeBorder.h*2;
	}

	// Get the size.
	Size size() {
		Float w = max(namesWidth + dataWidth + compositeBorder.w, caption.size.w);
		Float h = height + captionHeight;
		Size(w, h) + compositeBorder*2;
	}

	// Draw ourselves.
	void draw(Graphics g, Bool active) {
		g.fill(pos, dataBg);
		g.draw(pos, dataFg);

		Rect captionPos(pos.p0, Size(pos.size.w, caption.size.h + compositeBorder.h*2));
		g.fill(captionPos, captionBg);
		g.draw(captionPos, captionFg);
		g.draw(caption, captionFg, pos.p0 + compositeBorder);

		Point at = pos.p0 + compositeBorder;
		at.y = captionPos.p1.y + compositeBorder.h;
		for (v in vars) {
			var nameSz = v.text.size;
			var dataSz = v.value.size;

			if (active)
				g.draw(v.text, dataFg, at);
			v.value.pos.x = pos.p1.x - dataSz.w - compositeBorder.w;
			v.value.pos.y = at.y;
			v.value.draw(g, active);

			at.y += max(nameSz.h, dataSz.h) + dataBorder.h;
		}
	}

	void drawLinks(Graphics g, Bool active) {
		for (v in vars) {
			v.value.drawLinks(g, active);
		}
	}

private:
	// A variable inside a frame.
	class Var on Render {
		Str name;
		Text text;
		Data value;
		Type? originalType;

		init(Workspace ws, StackVar var) {
			unsafe:RawPtr ptr(var.value);

			init() {
				name = var.name;
				text = Text(var.name, dataFont);
				value = ws.createStackData(ptr);
				originalType = ptr.type;
			}

			update(ws, var);
		}

		void update(Workspace ws, StackVar var) {
			unsafe:RawPtr ptr(var.value);

			if (name != var.name) {
				name = var.name;
				text = Text(name, dataFont);
			}

			if (originalType !is ptr.type) {
				originalType = ptr.type;
				value = ws.createStackData(ptr);
			}

			value.update(ws, ptr, 0);
		}
	}

	// Values in here.
	Var[] vars;

	Float namesWidth;
	Float dataWidth;
	Float height;

	void updateSize() {
		namesWidth = 0;
		dataWidth = 0;
		height = 0;
		for (v in vars) {
			var nameSz = v.text.size;
			var dataSz = v.value.size;

			namesWidth = max(namesWidth, nameSz.w + dataBorder.w);
			dataWidth = max(dataWidth, dataSz.w);
			height += max(nameSz.h, dataSz.h) + dataBorder.h;
		}

		height -= dataBorder.h;
	}
}

