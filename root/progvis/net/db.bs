use core:io;
use sql;

DATABASE ProgvisDB {
	// Users in the system.
	TABLE users(
		// User id. Used in other tables.
		id INTEGER PRIMARY KEY,
		// User name.
		name TEXT ALLOW NULL,
		// User display name.
		displayName TEXT
	);

	// Known clients in the system, and what users they map to.
	TABLE clients(
		// Client ID (a long string).
		id TEXT PRIMARY KEY UNIQUE,
		// User ID.
		user INTEGER
	);

	// Problems in the system. There are two types of problems: one that only consists of a piece of
	// code, and one that has an interface + an implementation.
	TABLE problems(
		// ID of this problem.
		id INTEGER PRIMARY KEY,
		// Problem author (foreign key to users)
		author INTEGER,
		// Title of the problem.
		title TEXT,
		// What is the current state of this problem? I.e. what is the next thing to modify.
		// 0 = modify the implementation, 1 = modify the main program.
		next INTEGER,
		// Source code of the main program (to code table).
		main INTEGER,
		// Source code of the implementation (to code table).
		impl INTEGER,
		// Source code of the reference implementation (to code table).
		refimpl INTEGER,
		// Improved version of a previous problem, if any.
		improved INTEGER ALLOW NULL,
		// Created (time string in UTC)
		created TEXT
		);
	INDEX ON problems(author);
	INDEX ON problems(improved);

	// A piece of code for some problem.
	TABLE code(
		// ID of this piece of code.
		id INTEGER PRIMARY KEY,
		// Program source code.
		src TEXT,
		// Language (= file extension) of the problem.
		language TEXT
		);

}

class Database {
	init() {
		SQLite db(cwdUrl / "progvis.db");

		init() {
			db(db);
		}
	}

	private ProgvisDB db;

	// Find a user's identity from its client key.
	UserInfo? findUser(Str clientId) {
		if (x = WITH db: SELECT ONE users.id, users.displayName FROM clients JOIN users ON clients.user == users.id WHERE clients.id == clientId) {
			return UserInfo(x.users_id, x.users_displayName);
		}
		null;
	}

	// Find a user's name from its ID.
	Str? findUserName(Int userId) {
		if (x = WITH db: SELECT ONE displayName FROM users WHERE id == userId) {
			return x.displayName;
		} else {
			return null;
		}
	}

	// Log out a client.
	void logout(Str clientId) {
		WITH db: DELETE FROM clients WHERE id == clientId;
	}

	// Change username.
	void changeName(Int userId, Str newName) {
		WITH db: UPDATE users SET displayName = newName WHERE id == userId;
	}

	// Get a list of problems in the system. Excludes problems that the user has already solved
	// based on 'wantSolved'. 'wantMain' indicates if we want problems where the next step is to
	// modify the main function.
	ProblemInfo[] problems(Int forUser, Bool wantSolved, Bool wantMain) {
		ProblemInfo[] result;
		Int main = if (wantMain) { 1; } else { 0; };
		var x = WITH db: SELECT problems.id AS id, displayName, title FROM problems
			JOIN users ON problems.author == users.id
			WHERE author != forUser AND next == main;
		for (row in x) {
			var problemId = row.id;

			Bool solved = (WITH db: SELECT ONE id FROM problems WHERE improved == problemId AND author == forUser).any;
			if (solved == wantSolved) {
				result << ProblemInfo(problemId, row.title, row.displayName);
			}
		}

		result;
	}

	// Get a list of the user's own problems.
	Problem[] ownProblems(Int forUser) {
		Problem[] result;
		var x = WITH db: SELECT problems.id AS id, displayName, title FROM problems
			JOIN users ON problems.author == users.id
			WHERE author == forUser;
		for (row in x) {
			result << Problem(row.id, row.title, row.displayName, countSolved(row.id));
		}
		result;
	}

	private Nat countSolved(Int problemId) {
		0; // WITH db: COUNT FROM solutions WHERE to == problemId;
	}

	// Compute the points of all users in the database.
	Int->Int allScores() {
		Int->Int result;

		// // Points given to a solution that finds some error.
		// Int errorSolution = 5;
		// // Points given to a solution that don't find some error.
		// Int correctSolution = 1;
		// // Extra points if the solution was improved.
		// Int improved = 2;
		// // Points given to a problem that got an incorrect solution.
		// Int errorProblem = 1;
		// // Points given to a problem that got a correct solution.
		// Int correctProblem = 2;

		// var q = WITH db: SELECT
		// 	solutions.author AS author,
		// 	solutions.solution AS solution,
		// 	solutions.improved AS improved,
		// 	problems.author AS toAuthor
		// 	FROM solutions
		// 	JOIN problems ON solutions.to == problems.id;
		// for (row in q) {
		// 	Int pScore = 0;
		// 	Int sScore = 0;
		// 	if (row.solution) {
		// 		sScore += errorSolution;
		// 		pScore += errorProblem;
		// 	} else {
		// 		sScore += correctSolution;
		// 		pScore += correctProblem;
		// 	}

		// 	if (row.improved)
		// 		sScore += improved;

		// 	result[row.author] += sScore;
		// 	result[row.toAuthor] += pScore;
		// }

		result;
	}
}

class UserInfo {
	// User ID.
	Int id;

	// Display name of the user.
	Str name;

	init(Int id, Str name) {
		init { id = id; name = name; }
	}
}
