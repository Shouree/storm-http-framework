use ui;
use core:sync;
use core:lang;

/**
 * Represents a running thread inside the program.
 */
class ProgThread on Render {
	// Semaphore used to pause the monitored thread.
	Sema sema;

	// Current location. Set to null if the thread is currently running.
	SrcPos? pos;

	// Current call depth. There may be additional entries in 'frames', as we might have exited a
	// function but not yet received any new information. In this case, we will still show the old
	// state since it might contain references to values that are still known by the code, but not
	// otherwise reachable (eg. the return value).
	Nat callDepth;

	// Entries of called functions. We expect this array to contain at least one element when we
	// receive calls to 'onNewLocation'.
	StackFrame[] frames;

	// Owning Program object, so we can access the callback.
	Program owner;

	// Create.
	init(Program owner) {
		init() {
			sema(0);
			callDepth = 0;
			owner = owner;
		}
	}

	// Called by a monitored thread to possible pause execution.
	void onNewLocation(SrcPos pos, StackVar[] vars) {
		popFrames();

		// TODO: Decide if we should break.
		// print("Thread reached ${pos}");
		this.frames.last.variables = vars;
		this.pos = pos;
		owner.notifyChange();
		// sema.down();
		sleep(100 ms);
	}

	// Called when a new function was entered.
	void onFunctionEntered(Str name, StackVar[] vars) {
		// TODO: Set the position as well!
		popFrames();

		callDepth++;
		frames.push(StackFrame(name, vars));

		owner.notifyChange();
		// sema.down();
		sleep(100 ms);
	}

	// Called when a function is about to return.
	void onFunctionExited() {
		// print("Function ${frames.last.function} exited.");
		if (callDepth > 0)
			callDepth--;
	}

	// Resume the thread if it is not stopped.
	Bool resume() {
		if (pos) {
			this.pos = null;
			sema.up();
			true;
		} else {
			false;
		}
	}

	// Check if the thread is currently running.
	Bool running() {
		if (pos)
			true;
		else
			false;
	}

	// Decrease the stack frames until the proper depth is reached.
	private void popFrames() {
		while (frames.count > callDepth)
			frames.pop();
	}
}

/**
 * A variable in a stack-frame.
 *
 * Note: We fill in this type directly from machine code, don't alter the layout!
 */
value StackVar {
	Str name;
	Variant value;

	init(Str name, Variant value) {
		init { name = name; value = value; }
	}
}


/**
 * A stack frame of a thread.
 */
class StackFrame on Render {
	// Name of the function. (TODO: Add a SrcPos?)
	Str function;

	// The current state of all variables. Ordered roughly as they appear in the source.
	StackVar[] variables;

	// Create.
	init(Str name, StackVar[] vars) {
		init {
			function = name;
			variables = vars;
		}
	}
}
