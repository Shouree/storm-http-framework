use ui;
use layout;
use core:info;
use core:geometry;

dialog LicenseDialog {
	layout Grid {
		expandCol: 0;
		expandRow: 2;
		Label info("") {}
		nextLine;
		Label l("Licenses:") {}
		nextLine;
		ScrollWindow scroll(Licenses(), Size(100, 200)) {}
	}

	init(Str name, Version? version) {
		init("About ${name}", Size(200, 100)) {}

		if (version)
			info.text = "${name} ${version}";
		else
			info.text = name;

		if (buttons = scroll.content as Licenses) {
			for (l in core:info:licenses()) {
				buttons.add(l);
			}
		}
	}
}

class Licenses extends Container {
	init() {
		init() {}
	}

	// Buttons. One for each license.
	private Button[] buttons;

	// Add a button.
	void add(License l) {
		Button b(l.name);
		buttons << b;
		add(b);

		var me = this;
		b.onClick = (Button b) => me.showLicense(l);
	}

	// Show a license.
	void showLicense(License l) {
		print("Show the license!");
	}

	// Update size.
	void resized(Size size) : override {
		Float y = 0;
		for (b in buttons) {
			Size s = b.minSize;
			b.pos = Rect(0, y, size.w, y + s.h);
			y += s.h + 8;
		}
	}

	// Update minimum size.
	Size minSize() : override {
		Size min;
		for (b in buttons) {
			Size s = b.minSize;
			min.w = max(min.w, s.w);
			min.h += 8 + s.h;
		}
		min.h -= 8;
		min;
	}
}
