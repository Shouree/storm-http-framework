use lang.bs;
use core.lang;

optional delimiter = lang.bs.SDelimiter;
required delimiter = lang.bs.SRequiredDelimiter;

// This file contains the table declarations of the SQL plugin.

// TODO: We should maybe provide "sql name" token to allow for names with spaces in them.

// The top-level table declaration.
SPlainFileItem => DBDecl(name, env, contents) : "DATABASE" ~ lang.bs.SName name, "{" [, SDBContents contents, ]+ "}";


// Contents of a database.
DBContents SDBContents();
SDBContents => DBContents() : (SDBItem(me),)*;

// An item in a DB declaration.
void SDBItem(DBContents to);
SDBItem => to : STableDecl -> add;
// More here!

// Declaration of a single table.
TableDecl STableDecl();
STableDecl => TableDecl(name, columns) : "TABLE" ~ SName name, "(" [, SColumnDecls columns, ]+ ")", ";";

// Column declarations.
Array<ColumnDecl> SColumnDecls();
SColumnDecls => Array<ColumnDecl>() : SColumnDecl -> push - (, ",", SColumnDecl -> push)*;

ColumnDecl SColumnDecl();
SColumnDecl => ColumnDecl(name, type, modifiers) : SName name ~ SDataType type -(~ SModifier modifiers)*;


// Data types in table declarations.
SQLType SDataType() #typeName;
SDataType => sqlInteger() : "INTEGER";
SDataType => sqlReal() : "REAL";
SDataType => sqlText() : "TEXT";


// Modifiers for columns.
Str SModifier() #keyword;
SModifier => t : "PRIMARY KEY" t;
SModifier => t : "FOREIGN KEY" t;
SModifier => t : "NOT NULL" t;
SModifier => t : "UNIQUE" t;
SModifier => t : "AUTOINCREMENT" t;

