use lang.bs;
use core.lang;

optional delimiter = lang.bs.SDelimiter;
required delimiter = lang.bs.SRequiredDelimiter;

// This file contains the table declarations of the SQL plugin.

// TODO: We should maybe provide "sql name" token to allow for names with spaces in them.

// The top-level table declaration.
SPlainFileItem => DatabaseDecl(name, env, contents) : "DATABASE" ~ lang.bs.SName name, "{" [, SDatabase contents, ]+ "}";


// Contents of a database.
Database SDatabase();
SDatabase => Database() : (SDBItem(me),)*;

// An item in a DB declaration.
void SDBItem(Database to);
SDBItem => to : STable -> add;
SDBItem => to : SIndex -> add;
// More here!

// Declaration of an index.
IndexDecl SIndex();
SIndex => IndexDecl(pos, name, table, cols) : "INDEX" ~ SName name ~ "ON" ~ SName table, "(", SNameList cols, ")", ";";
SIndex => IndexDecl(pos, table, cols) : "INDEX" ~ "ON" ~ SName table, "(", SNameList cols, ")", ";";

// Declaration of a single table.
Table STable();
STable => Table(name) : "TABLE" ~ SName name, "(" [, STableContent(me), ]+ ")", ";";

// Table content.
void STableContent(Table to);
STableContent => to : STableItem(to) - (, ",", STableItem(to))*;

// Something we can declare inside a table.
void STableItem(Table to);
STableItem => to : SColumn -> add;
STableItem => to : SPKDecl -> add;

// Declaration of a primary key.
Array<SStr> SPKDecl();
SPKDecl => cols : "PRIMARY" ~ "KEY", "(", SNameList cols, ")";

Column SColumn();
// Note: Using SModifiers here is wrong due to whitespace.
SColumn => Column(name, type) : SName name ~ SDatatype type - (~ SModifier(me))*;

// Data types in table declarations.
SQLType SDatatype() #typeName;
SDatatype => sqlInteger() : "INTEGER";
SDatatype => sqlReal() : "REAL";
SDatatype => sqlText() : "TEXT";

// Parse zero or more modifiers.
void SModifiers(Column c);
SModifiers : ;
SModifiers : SModifier(c) - (~ SModifier(c))*;

// Modifiers for columns.
void SModifier(Column to) #keyword;
SModifier => to : "PRIMARY" ~ "KEY" -> setPrimary;
SModifier => to : "NOT" ~ "NULL" -> setNotNull;
SModifier => to : "UNIQUE" -> setUnique;
SModifier => to : "AUTOINCREMENT" -> setAutoIncrement;

