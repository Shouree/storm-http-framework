// Syntax extensions useful when generating code or manipulating the syntax tree.
use lang.bs;

delimiter = SDelimiter;

// Use type{a:b:c} to build a type name quickly and easily.
SExpr => createType(pos, block, type) : "name" #keyword, "{", SType type, "}";

// Use named{a:b:c} to refer to objects. Evaluates compile-time to the type of the actual object
// in the compiler.
SExpr => referNamed(pos, block, name) : "named" #keyword, "{", SType name, "}";

// Output a type.
SExpr => exploreType(pos, block, type) : "explore" #keyword, "{", SType type, "}";

// Reload a package (TODO: Nicer syntax!)
SExpr => reloadName(block, type) : "reload" #keyword, "{", SType type, "}";

// Debug things by printing expressions at compile-time.
SExpr => printExpr(t, stmt) : "dump" #keyword, "{", (SStmt(block) stmt) t, "}";
SExpr => printExpr(t, expr) : "dump" #keyword, "{", (SExpr(block) expr) t, "}";

// Debug asm by printing asm at compile-time.
SExpr[100] => PrintAsm(pos) : "dumpAsm" #keyword;


// Generate things in classes and at top-level.
SFileItem => Generate(env, name, param, body) :
    SName name #fnName, ":", "generate" #keyword, "(", SName param #varName, ")", "{", SFunctionBody@ body, "}";
SClassItem => Generate(owner, name, param, body) :
    SName name #fnName, ":", "generate" #keyword, "(", SName param #varName, ")", "{", SFunctionBody@ body, "}";

// Pattern-based block- and expr generation.
SExpr => TemplateExpr(block, env, expr) : SPattern(block) env, SExpr @expr;

Expr SPattern(Block block);
SPattern => env : "pattern" #keyword, "(", SExpr(block) env, ")";

// Inserting expressions into templates.
SAtom => insertExpr(pos, block) : "$" #keyword - SPatternExpr @pattern = PatternAtom;

// Insert zero or more expressions into a parameter list...
SActual => insertActuals(pos, to, block) : "@" #keyword - SPatternExpr @pattern = PatternActual;
// .. or inside array initializations...
SArrayInitItem => insertExprs(pos, to, block) : "@" #keyword - SPatternExpr @pattern = PatternArray;
// .. or inside a regular block.
SBlockItem => insertExprs(pos, parent) : "@" #keyword - SPatternExpr @pattern = PatternBlockItem;

void SPatternExpr();
SPatternExpr : SName name #keyword = PatternVar;
SPatternExpr : "{" #keyword, SExpr @expr, "}" #keyword = PatternExpr;
