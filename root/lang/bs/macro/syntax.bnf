// Syntax extensions useful when generating code or manipulating the syntax tree.
use lang.bs;

delimiter = SDelimiter;

// Use type{a:b:c} to build a type name quickly and easily.
SAtom => createType(pos, block, type) : "name" #keyword, "{", SType type, "}";

// Use named{a:b:c} to refer to objects. Evaluates compile-time to the type of the actual object
// in the compiler.
SAtom => referNamed(pos, block, name) : "named" #keyword, "{", SType name, "}";

// Output a type.
SExpr => exploreType(pos, block, type) : "explore" #keyword, "{", SType type, "}";

// Reload a package (TODO: Nicer syntax!)
SExpr => reloadName(block, type) : "reload" #keyword, "{", SType type, "}";

// Debug things by printing expressions at compile-time.
SExpr => printExpr(t, stmt) : "dump" #keyword, "{", (SStmt(block) stmt) t, "}";
SExpr => printExpr(t, expr) : "dump" #keyword, "{", (SExpr(block) expr) t, "}";

// Debug asm by printing asm at compile-time.
SExpr[100] => PrintAsm(pos) : "dumpAsm" #keyword;


// Generate things in classes and at top-level.
SFileItem => Generate(env, name, param, body) :
    SName name #fnName, ":", "generate" #keyword, "(", SName param #varName, ")", "{", SFunctionBody@ body, "}";
SClassItem => Generate(owner, name, param, body) :
    SName name #fnName, ":", "generate" #keyword, "(", SName param #varName, ")", "{", SFunctionBody@ body, "}";

// Pattern-based block- and expr generation. TODO: It would be nice to have whole function generation as well!
SExpr => TemplateExpr(block, env, body) : "pattern" #keyword, "(", SExpr(block) env, ")", SPatternBody body;

// Expression inside a pattern. We only allow the special syntax inside here!
SExpr SPatternBody();
SPatternBody => e : SExpr @e;

// Inserting expressions into templates.
SPatternBody..SAtom => insertExpr(pos, block, pattern) : "$" #keyword - SPatternExpr @pattern = PatternAtom;

// Insert zero or more expressions into a parameter list...
SPatternBody..SActual => insertActuals(pos, to, block, pattern) : "@" #keyword - SPatternExpr @pattern = PatternActual;
// .. or inside array initializations...
SPatternBody..SArrayInitItem => insertExprs(pos, to, block, pattern) : "@" #keyword - SPatternExpr @pattern = PatternArray;
// .. or inside a regular block.
SPatternBody..SBlockItem => insertExprs(pos, parent, pattern) : "@" #keyword - SPatternExpr @pattern = PatternBlockItem;

void SPatternExpr();
SPatternExpr : SName name #keyword = PatternVar;
SPatternExpr : "{" #keyword, SExpr @expr, "}" #keyword = PatternExpr;
