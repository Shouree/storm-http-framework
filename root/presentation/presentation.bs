use ui;
use core:geometry;
use graphics;

/**
 * A presentation.
 *
 * A presentation consists of a number of Slides, each of which contains a number of Elements to be
 * laid out in a certain way and displayed on the screen. Each slide may contain animations that
 * alter the state or layout of elements on the screen.
 *
 * In addition to the slides, the Presentation object contains global settings and resources for the
 * presentation in its entirety, such as background color, page numbering etc.
 */
class Presentation on Render {
	// Title of the presentation.
	Str title;

	// Size of the slides.
	Size size;

	// Border around slides.
	Size border;

	// All slides in the presentation.
	private Slide[] slides;

	// The background.
	private Element myBackground;

	// Create.
	init(Str title) {
		Size size(640, 480);
		Size border(10, 10);

		init() {
			title = title;
			size = size;
			border = border;
			myBackground = SolidFill(white);
		}

		myBackground.pos = Rect(size);
	}

	// An element used to paint the background.
	Element background() { myBackground; }

	// Set the background element.
	assign background(Element e) {
		myBackground = e;
		myBackground.pos = Rect(size);
	}

	// Add a slide.
	void add(Slide s) {
		slides << s;
		s.added(this);
	}

	// Draw the presentation in its current state. Assumes 'g' has been setup to match the slide
	// size.
	void draw(Graphics g) {
		background.draw(g);

		if (slides.any)
			slides.first.draw(g, 0, 0);
	}

}
