use ui;
use layout;
use geometry;

/**
 * A slide in a presentation.
 *
 * Contains a number of elements laid out according to the current layout.
 *
 * A slide is scaled to occupy 1024x768 virtual pixels regardless of the actual size of the output.
 */
class Slide on Render {
	// All elements in this slide.
	Element[] elements;

	// The layout for this slide.
	private Layout layout;

	// Create without a layout.
	init() {
		init() {}
	}

	// Create, providing a layout. Traverses the layout to find all elements.
	init(Layout layout) {
		init() {
			layout = layout;
		}

		layout.findAll(&this.addElement);
	}

	// Called when we're added to the presentation so that we get a chance to perform layout.
	package void added(Presentation to) {
		layout.pos(Rect(to.size));
	}

	// Draw this slide. 'animation' is the animation number currently being played, and 'time' is in
	// the range 0-1 where 1 is complete. Animation number 0 is the intro animation of the entire
	// slide.
	void draw(Graphics g, Nat animation, Float time) {
		// TODO: Consider animations!

		for (element in elements) {
			element.draw(g);
		}
	}

private:
	// Add an element if we know about it.
	void addElement(Component c) {
		if (c as ElementWrap) {
			elements << c.control;
		}
	}
}
