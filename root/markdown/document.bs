/**
 * Root representation of a Markdown document.
 */
class Document {
	// Create an empty document.
	init(ElementSeq elements) {
		init { elements = elements; }
	}

	// Elements in the document.
	ElementSeq elements;

	// To string.
	protected void toS(StrBuf to) : override {
		elements.toS(to);
	}

	// To HTML.
	Html toHtml() {
		Html out;
		elements.toHtml(out);
		out;
	}
}

/**
 * Collection of elements.
 */
class ElementSeq {
	// Elements.
	Element[] elements;

	// Create.
	init() {
		init {}
	}

	// Add an element.
	void add(Element e) {
		elements << e;
	}

	// To string.
	void toS(StrBuf to) : override {
		to << join(elements, "\n\n");
	}

	// To HTML.
	void toHtml(Html to) {
		for (i, element in elements) {
			if (i > 0)
				to.html("\n");
			element.toHtml(to);
		}
	}

	// To HTML, ignore first paragraph.
	void toHtmlInline(Html to) {
		for (i, element in elements) {
			if (i == 0) {
				if (element as Paragraph) {
					element.text.toHtml(to);
					continue;
				}
			} else {
				to.html("\n");
			}

			element.toHtml(to);
		}
	}
}


/**
 * Element in a document.
 */
class Element {
	// To HTML.
	void toHtml(Html to) : abstract;
}


/**
 * Heading.
 */
class Heading extends Element {
	// Heading level.
	Nat level;

	// Text.
	FormattedText text;

	// Create.
	init(Nat level, FormattedText text) {
		init { level = level; text = text; }
	}

	// Output.
	protected void toS(StrBuf to) : override {
		to << "#"*level << " " << text;
	}

	// To HTML.
	void toHtml(Html to) : override {
		to.html("<h${level}>");
		text.toHtml(to);
		to.html("</h${level}>");
	}
}


/**
 * A paragraph of text.
 */
class Paragraph extends Element {
	// The text in the paragraph.
	FormattedText text;

	// Create.
	init(FormattedText text) {
		init { text = text; }
	}

	// Output.
	protected void toS(StrBuf to) : override {
		to << text;
	}

	// To HTML.
	void toHtml(Html to) : override {
		to.html("<p>\n");
		to.indent();
		text.toHtml(to);
		to.dedent();
		to.html("\n</p>");
	}
}


/**
 * A generic list.
 */
class List extends Element {
	// Ordered?
	Bool ordered;

	// Contents of the list.
	ElementSeq[] items;

	// Create.
	init(Bool ordered) {
		init { ordered = ordered; }
	}

	// Add an element. Content can be added later.
	void add(ElementSeq item) {
		items << item;
	}

	// To string.
	protected void toS(StrBuf to) : override {
		if (ordered) {
			Nat width = 0;
			for (x in items.count.toS)
				width++;

			to.indentBy(" "*(width + 2));
			for (i, e in items) {
				if (i > 0)
					to << "\n\n";
				to << width(width) << fill(Char(48)) << (i + 1) << ". ";
				{
					Indent z(to);
					e.toS(to);
				}
			}
		} else {
			to.indentBy("  ");
			for (i, e in items) {
				if (i > 0)
					to << "\n\n";
				to << "- ";
				{
					Indent z(to);
					e.toS(to);
				}
			}
		}
	}

	void toHtml(Html to) : override {
		if (ordered)
			to.html("<ol>\n");
		else
			to.html("<ul>\n");
		to.indent();
		for (e in items) {
			to.html("<li>\n");
			to.indent();
			e.toHtmlInline(to);
			to.html("\n");
			to.dedent();
			to.html("</li>\n");
		}
		to.dedent();
		if (ordered)
			to.html("</ol>");
		else
			to.html("</ul>");
	}
}
