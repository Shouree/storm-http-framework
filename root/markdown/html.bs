/**
 * HTML export.
 *
 * Wraps a StrBuf, and automatically escapes HTML characters as necessary.
 */
class Html {
	// Output buffer.
	private StrBuf out;

	// Characters we need to escape.
	private Char->Str escape;

	// Create.
	init() {
		init {}

		// For text:
		escape.put('&', "&amp;");
		escape.put('<', "&lt;");
		escape.put('>', "&gt;");
		// Inside string attributes:
		escape.put('"', "&quot;");
		escape.put('\'', "&#39;");
	}

	// To string.
	Str toS() {
		out.toS;
	}

	// Indent output.
	void indent() {
		out.indent();
	}

	void dedent() {
		out.dedent();
	}

	// Append text. Escapes HTML characters.
	Html text(Str text) {
		for (ch in text) {
			if (escape.has(ch))
				out << escape.get(ch);
			else
				out << ch;
		}
		this;
	}

	// Append plain HTML. No escapes.
	Html html(Str html) {
		out << html;
		this;
	}
}
