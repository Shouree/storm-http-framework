use ui;
use graphics;
use core:io;
use core:geometry;
use lang:bs:macro;

class SimpleWin extends Frame {
	Label lbl;
	Button button;
	Edit edit;
	Int times;

	init() {
		init("Window", Size(200, 200)) {
			lbl("Input text:");
			edit("text");
			button("OK");
			times = 0;
		}

		lbl.pos(Rect(10, 10, 190, 30));
		edit.pos(Rect(10, 30, 190, 60));
		button.pos(Rect(10, 100, 190, 190));

		button.onClick = &this.onOk(Button);
		add(lbl);
		add(edit);
		add(button);
		create();
	}

	void onOk(Button b) {
		lbl.text("You entered: " + edit.text);
		button.text("Clicked" + toS(times++));
	}
}

class DrawWin extends Frame {
	init() {
		init("Window", Size(200, 200));
		painter(SimpleDraw());
		create();
	}

	Bool onKey(Bool down, Key key, Modifiers mod) {
		print("Keycode: " # down # ", " # key # ", " # mod);
		close();
		false;
	}
}

Url resUrl() {
	if (u = (named{res}).url) {
		u;
	} else {
		Url();
	}
}

class SimpleDraw extends Painter {
	Path star;
	LinearGradient bgBrush;
	SolidBrush starColor;
	SolidBrush solid;
	SolidBrush textColor;
	Moment started;
	Text text;
	Size textSize;
	Bitmap image;

	init() {
		init() {
			bgBrush(red, green, 45 deg);
			// bgBrush([GradientStop(0, red), GradientStop(0.5, green), GradientStop(1, blue)], 45 deg);
			starColor = yellow;
			solid = green;
			textColor = blue;
			text = Text("Hello!", defaultFont);
			image = graphics:loadImage(resUrl / "sample.jpg");
		}
		bgColor = red;

		textSize = text.size();
		text.layoutBorder(textSize);

		Int edges = 5;
		Int stops = edges * 2;
		for (Int i = 0; i < stops; i++) {
			Angle a = 360 deg / stops.float;
			a *= i.float;

			Point p = angle(a) * 20.0;
			if (i % 2 == 0)
				p *= 2.0;
			star.point(p);
		}
		star.close();
	}

	Bool render(Size size, Graphics to) {
		Moment now;
		Float time = (now - started) / 30 ms;

		to.fill(bgBrush);
		// to.transform(rotate(20 deg, Point(100, 100)));

		to.line(Point(0, 0), Point(100, 100), solid);
		to.draw(Rect(10, 10, 190, 190), solid);
		to.draw(Rect(20, 20, 180, 180), Size(50), solid);
		to.oval(Rect(40, 40, 100, 80), solid);

		to.push(Rect(0, 0, 120, 120), 0.5);
		to.transform(translate(Point(100, 100)));
		to.fill(star, starColor);

		to.push();
		to.transform(rotate(1 deg * time));
		to.fill(star, starColor);
		to.draw(text, textColor, Point() - (textSize / 2));

		to.pop();
		to.pop();

		to.transform(Transform());
		to.draw(image, Point(100, 100));

		true;
	}

}

void simple() {
	// SimpleWin win;
	DrawWin win;
	win.waitForClose();
	// sleep(2 s);
	// win.close();
	print("Bye!");
}
