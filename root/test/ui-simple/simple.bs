use ui;
use graphics;
use core:geometry;

class SimpleWin extends Frame {
	Label lbl;
	Button button;
	Edit edit;
	Int times;

	init() {
		init("Window", Size(200, 200)) {
			lbl("Input text:");
			edit("text");
			button("OK");
			times = 0;
		}

		lbl.pos(Rect(10, 10, 190, 30));
		edit.pos(Rect(10, 30, 190, 60));
		button.pos(Rect(10, 100, 190, 190));

		button.onClick = &this.onOk(Button);
		add(lbl);
		add(edit);
		add(button);
		create();
	}

	void onOk(Button b) {
		lbl.text("You entered: " + edit.text);
		button.text("Clicked" + toS(times++));
	}
}

class DrawWin extends Frame {
	init() {
		init("Window", Size(200, 200));
		painter(SimpleDraw());
		create();
	}

	Bool onKey(Bool down, Nat keycode, Modifiers mod) {
		print("Keycode: " # down # ", " # keycode # ", " # mod);
		close();
		false;
	}
}

class SimpleDraw extends Painter {
	Path star;
	LinearGradient bgBrush;
	SolidBrush starColor;
	SolidBrush solid;

	init() {
		init() {
			bgBrush(red, green, 45 deg);
			starColor = yellow;
			solid = green;
		}

		Int edges = 5;
		Int stops = edges * 2;
		for (Int i = 0; i < stops; i++) {
			Angle a = 360 deg / stops.float;
			a *= i.float;

			Point p = angle(a) * 20.0;
			if (i % 2 == 0)
				p *= 2.0;
			star.point(p);
		}
		star.close();
	}

	Bool render(Size size, Graphics to) {
		to.fill(bgBrush);
		to.line(Point(0, 0), Point(100, 100), solid);
		to.draw(Rect(10, 10, 190, 190), solid);
		to.draw(Rect(20, 20, 180, 180), Size(50), solid);
		to.oval(Rect(40, 40, 100, 80), solid);

		to.draw(star, starColor);

		false;
	}

}

void simple() {
	// SimpleWin win;
	DrawWin win;
	win.waitForClose();
	// sleep(2 s);
	// win.close();
	print("Bye!");
}
