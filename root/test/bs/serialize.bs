use lang:bs;
use lang:bs:macro;
use core:io;
use util:serialize;

/**
 * Serializable class.
 */
class Data : persist {
	Int a;
	Int b;
	Employee data1;
	Employee data2;

	init() {
		init() {
			a = 5;
			b = 8;
			data1 = Employee();
			data2 = Manager();
		}
	}

	void toS(StrBuf to) {
		to << "{ a: " << a << ", b: " << b << ", data1: " << data1 << ", data2: " << data2 << " }";
	}

	// Write the members in another order. Should still be readable by 'read'.
	void writeShuffled(ObjOStream to) {
		SerializedStdType desc(named{Data}, &__init(Data, ObjIStream));
		desc.add("b", named{core:Int});
		desc.add("data2", named{Employee});
		desc.add("a", named{core:Int});
		desc.add("data1", named{Employee});

		if (to.startObject(desc, this)) {
			b.write(to);
			data2.write(to);
			a.write(to);
			data1.write(to);
			to.end();
		}
	}
}

/**
 * Another serializable class.
 */
class Employee : persist {
	Str name;
	Int coolness;

	init() {
		init() { name = "John Smith"; coolness = 500; }
	}

	void toS(StrBuf to) {
		to << "Employee: { name: " << name << ", coolness: " << coolness << " }";
	}
}


/**
 * A sub-class to Employee.
 */
class Manager : extends Employee, persist {
	Str title;

	init() {
		init() { title = "Executive"; }
		name = "Mr. " # name;
		coolness *= 10;
	}

	void toS(StrBuf to) {
		to << "Manager: { name: " << name << ", coolness: " << coolness << ", title: " << title << " }";
	}
}

// Deserialize things.
void deserialize(MemOStream from) {
	{
		MemIStream in(from.buffer);
		TextObjStream read(in);
		print("Result: " # read.read());
	}

	{
		MemIStream in(from.buffer);
		ObjIStream read(in);
		print("Deserialization: " # Data:read(read));
	}
}

// Test serialization.
void testSerialize() {
	Data x;

	{
		MemOStream out;
		ObjOStream obj(out);
		x.write(obj);
		deserialize(out);
	}

	print("Shuffled output:");

	{
		MemOStream out;
		ObjOStream obj(out);
		x.writeShuffled(obj);
		deserialize(out);
	}
}
