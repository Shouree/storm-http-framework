use ui;
use core:geometry;
use core:debug;

class MainWin extends Frame {
	Label lbl;
	Edit input;
	Button btn;

	ctor() {
		init("Test window") {
			lbl("Name:");
			input("Your name");
			btn("Click me!");
		}
		size(Size(150, 150));

		add(lbl);

		input.onReturn = &this->enterPressed(Edit);
		add(input);

		btn.onClick = &this->btnClicked(Button);
		add(btn);

		create();
	}

	void resized(Size s) {
		Size border = 8;
		Point w = border;
		Size sz = s - 2*border;

		lbl.pos(Rect(w, Size(sz.w, 20)));
		w.y = w.y + 28;
		sz.h = sz.h - 28;

		input.pos(Rect(w, Size(sz.w, 20)));
		w.y = w.y + 28;
		sz.h = sz.h - 28;

		btn.pos(Rect(w, sz));
	}

	void btnClicked(Button btn) {
		btn.text("Well done: " + input.text);
	}

	void enterPressed(Edit edit) {
		btn.text("Well edited: " + input.text);
	}
}

thread Waiter;

void closeLater(MainWin w) on Waiter {
	dbgSleep(6000);
	w.close;
}

void main() {
	renderMgr;
	MainWin win;
	spawn closeLater(win);
	win.waitForClose;
}
