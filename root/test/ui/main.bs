use ui;
use core:geometry;
use core:io;
use graphics;
use core:debug;

class MainPainter extends Painter {
	SolidBrush brush;
	SolidBrush brush2;
	LinearGradient brush3;
	Moment started;
	Bitmap bitmap;
	Font font;

	ctor() {
		Url f = rootUrl / "res" / "ui_alpha.png";
		Image img = graphics:loadImage(f);

		init() {
			brush = red;
			brush2 = blue;
			brush3([GradientStop(0, red), GradientStop(1, green)], Point(0, 0), Point(1, 1));
			bitmap = img;
			font = defaultFont;
		}

		font.height(font.height * 3);
	}

	Bool render(Size size, Graphics g) {
		Point from(0, 0);
		Point to(size.w, size.h);
		Size border = 10;

		g.line(from, to, brush);
		g.line(Point(0, size.h), Point(size.w, 0), brush2);
		g.fillOval(Rect(from + border, to - border), brush3);

		Moment now;
		Float y = (now - started).inMs.float / 30.0;
		Point center = size / 2;
		g.transform(rotate(y * 5 deg, center));

		g.line(Point(0, y), Point(size.w, y), brush);

		g.push();
		g.transform(translate(Point(y, y)) * rotate(45 deg));
		g.draw(bitmap);
		g.pop();

		g.text("Hello World!", font, brush2, Rect(from, to));

		true;
	}
}

class PaintWin extends Frame {
	ctor() {
		init("Graphics!") {}
		size(Size(400, 400));
		painter(MainPainter());

		create();
	}
}

class MainWin extends Frame {
	Label lbl;
	Edit input;
	Button btn;

	ctor() {
		init("Test window") {
			lbl("Name:");
			input("Your name");
			btn("Click me!");
		}
		size(Size(200, 200));

		add(lbl);

		input.onReturn = &this->enterPressed(Edit);
		add(input);

		btn.onClick = &this->btnClicked(Button);
		add(btn);

		create();
	}

	void resized(Size s) {
		Size border = 8;
		Point w = border;
		Size sz = s - 2*border;

		lbl.pos(Rect(w, Size(sz.w, 20)));
		w.y = w.y + 28;
		sz.h = sz.h - 28;

		input.pos(Rect(w, Size(sz.w, 20)));
		w.y = w.y + 28;
		sz.h = sz.h - 28;

		btn.pos(Rect(w, Size(sz.w, 20)));
	}

	void btnClicked(Button btn) {
		btn.text("Well done: " + input.text);
	}

	void enterPressed(Edit edit) {
		btn.text("Well edited: " + input.text);
	}
}

thread Waiter;

void closeLater(Frame w) on Waiter {
	dbgSleep(6000);
	w.close;
}

void main() {
	// MainWin win;
	PaintWin win;
	spawn closeLater(win);
	win.waitForClose;
}
