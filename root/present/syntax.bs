use lang:bs;
use lang:bs:macro;
use core:lang;
use core:debug;

/**
 * Presentation expression
 */
class PExpr extends Expr {
	ExprBlock outer;
	ExprBlock into;
	LocalVar pVar;

	ctor(Block p, SStr title) {

		ExprBlock o(p);
		ExprBlock b(o);

		SStr varName = "presentation";
		Var var(o, name{present:Presentation}, varName, Actual(strConstant(title)));
		o.add(var);

		init() {
			outer = o;
			into = b;
			pVar = var.var;
		}

		outer.add(into);
		outer.add(LocalVarAccess(pVar));
	}

	void add(Expr e) {
		into.add(namedExpr(into, SStr("add"), LocalVarAccess(pVar), Actual(e)));
	}

	ExprResult result() {
		outer.result();
	}

	void code(CodeGen state, CodeResult to) {
		outer.code(state, to);
	}

	Str toS() {
		outer.toS();
	}
}

class SExpr extends Expr {
	ExprBlock outer;
	ExprBlock into;
	LocalVar sVar;

	ctor(PExpr p, SStr type, Actual params, IntroExpr intro) {
		ExprBlock o(p.into);
		ExprBlock b(o);

		SStr varName = "slide";
		SrcName name;
		name.add(type.v + "Slide");

		Var var(o, name, varName, params);
		o.add(var);

		init() {
			outer = o;
			into = b;
			sVar = var.var;
		}

		o.add(into);
		intro.add(o, LocalVarAccess(sVar));
		o.add(LocalVarAccess(sVar));
	}

	void add(ItemInfo i) {
		Str name = if (i.direct) { "add"; } else { "addDelayed"; };
		Actual actual(i.item);
		Expr? time = i.time;
		if (time)
			actual.add(time);
		into.add(namedExpr(into, SStr(name), LocalVarAccess(sVar), actual));
	}

	ExprResult result() {
		outer.result();
	}

	void code(CodeGen state, CodeResult to) {
		outer.code(state, to);
	}

	Str toS() {
		outer.toS();
	}
}

class IntroExpr {
	// Name of the transition (if any).
	Str? name;
	Actual params;

	ctor() {
		init() {}
	}

	ctor(SStr name, Actual params) {
		init() {
			name = name.v # "Intro";
			params = params;
		}
	}

	void add(ExprBlock b, Expr to) {
		if (name) {
			Expr param = namedExpr(b, SStr(name), params);
			b.add(namedExpr(b, SStr("setIntro"), to, Actual(param)));
		}
	}
}

class ItemInfo {
	Expr item;

	Bool direct;
	Expr? time;

	ctor(Expr item, Bool direct) {
		init() {
			item = item;
			direct = direct;
		}
	}

	ctor(Expr item, Expr time) {
		init() {
			item = item;
			direct = false;
			time = time;
		}
	}
}

ItemInfo itemExpr(Block block, SStr name, Actual params) {
	name.v = name.v + "Item";
	ItemInfo(namedExpr(block, name, params), true);
}

ItemInfo itemClickExpr(Block block, SStr name, Actual params) {
	name.v = name.v + "Item";
	ItemInfo(namedExpr(block, name, params), false);
}

ItemInfo itemTimeExpr(Block block, Expr time, SStr name, Actual params) {
	name.v = name.v + "Item";
	ItemInfo(namedExpr(block, name, params), time);
}
