use ui;
use core:geometry;
use core:debug;
use core:lang;
use lang:bs:macro;
use graphics;

/**
 * Main presentation window.
 */
class PresentWindow extends Frame {
	// Painter.
	private PresentPainter paint;

	// Package to reload.
	private Package toReload;

	// Number typed currently.
	private Nat typedNumber;

	init(Presentation show) {
		init(show.title) {
			paint(show);
			typedNumber = 0;
			toReload = show.reload;
		}

		cursorVisible(false);

		// size(Size(640, 480));
		size(Size(1024, 768));
		painter(paint);

		if (show.showTimer) {
			setTimer(5 s);
		}

		create();
	}

	// Key handling.
	Bool onKey(Bool down, Key id, Modifiers mod) {
		if (down) {
			if (id == Key:return) {
				if (typedNumber != 0) {
					paint.jumpTo(typedNumber - 1);
					typedNumber = 0;
				} else {
					paint.next;
				}
			} else if (id == Key:right) {
				spawn paint.next;
			} else if (id == Key:left) {
				spawn paint.prev;
			} else if (id == Key:esc) {
				close;
			} else if (id == Key:r) {
				paint.resetTimer;
				repaint;
			} else if (id == Key:s) {
				paint.surpriseKey;
			} else if (id == Key:f) {
				if (fullscreen) {
					fullscreen(false);
				} else {
					fullscreen(true);
				}
			} else if (keycode(id) >= 48 & keycode(id) <= 57) {
				typedNumber *= 10;
				typedNumber += keycode(id) - 48;
			} else if (id == Key:F5) { // F5 - reload. Remove when the emacs integration can support this!
				print("Reloading " # toReload.path);
				reloadPkg(toReload);
				print("Compiling...");
				compile(toReload);
				print("Done!");
				repaint;
			} else {
				print("Key: " # id);
			}
		}
		super:onKey(down, id, mod);
	}

	// Repaint, the timer on screen needs to be redrawn.
	void onTimer() {
		repaint();
	}

}


/**
 * Painter for the presentation window.
 */
class PresentPainter extends Painter {
	// The presentation.
	private Presentation p;

	// Current slide #.
	private Nat slideNr;

	// Current slide.
	private Slide? slide;

	// Previous slide (for animations).
	private Slide? lastSlide;

	// Current intro.
	private SlideIntro? intro;

	// End of presentation brush.
	private SolidBrush endColor;

	// Style.
	private SlideStyle style;

	// Brush used outside the slide's limit.
	private SolidBrush outsideColor;

	init(Presentation p) {
		init() {
			p = p;
			slideNr = 0;
			style(p);
			endColor = black;
			outsideColor = black;
		}

		if (p.slides.count > 0)
			show(p.slides[0]);
	}

	void jumpTo(Nat n) {
		var max = p.slides.count;
		if (n >= max) {
			slideNr = max;
			show(null);
		} else {
			slideNr = n;
			show(p.slides[n]);
		}
	}

	void next() {
		if (advance() == false) {
			Nat n = slideNr + 1;
			if (n >= p.slides.count) {
				slideNr = p.slides.count;
				show(null);
			} else {
				slideNr = n;
				show(p.slides[n]);
			}
		}
	}

	void prev() {
		if (slideNr > 0) {
			show(p.slides[--slideNr]);
		}
	}

	// Reset the timer.
	void resetTimer() {
		style.resetTimer();
		repaint();
	}

	// Surprise key pressed.
	void surpriseKey() {
		if (slide)
			slide.onSurprise();
	}

	// Advance the current slide.
	private Bool advance() {
		if (slide) {
			slide.doAnimation(true);
		} else {
			false;
		}
	}

	// Show a slide.
	private void show(Slide? s) {
		lastSlide = slide;
		slide = s;
		if (slide) {
			intro = slide.intro;
			slide.owner = this;

			// Start the intro first, otherwise we may repaint the intro before it has been properly
			// started due to animations in 'show'.
			if (intro) {
				intro.start;
			}
			slide.show();
		}

		repaint();
	}

	Bool render(Size s, Graphics g) {
		Size target(1024, 768);
		Bool animate = false;

		if (slide) {
			g.fill(outsideColor);

			Float scaleFactor = s.h / target.h;
			if (s.w / target.w < scaleFactor)
				scaleFactor = s.w / target.w;

			Point p = s;
			Size scaled = target * scaleFactor;
			g.transform(scale(scaleFactor) * translate(s.center - scaled.center));

			Rect targetRect(Point(), target);
			g.push(targetRect);
			animate |= slide.animate();

			if (intro) {
				animate = true;
				Bool more = intro.draw(lastSlide, slide, style, target, g);
				if (more == false)
					this.intro = null;
			} else {
				animate |= slide.draw(style, target, g);
			}
		} else {
			g.fill(endColor);
		}

		animate;
	}

}
