use ui;
use core:geometry;
use core:debug;
use graphics;

/**
 * Main presentation window.
 */
class PresentWindow extends Frame {
	// Painter.
	PresentPainter paint;

	ctor(Presentation show) {
		init(show.title) {
			paint(show);
		}

		cursorVisible(false);

		// size(Size(640, 480));
		size(Size(1024, 768));
		painter(paint);

		create();
	}

	// Closed.
	void close() {
		paint.cleanup();
		super:close();
	}

	// Key handling.
	Bool onKey(Bool down, Nat id) {
		if (down) {
			if (id == 13 | id == 39) {
				paint.next;
			} else if (id == 37) {
				paint.prev;
			} else if (id == 27) {
				close;
			} else if (id == 83) { // s
				paint.surpriseKey;
			} else if (id == 70) { // f
				if (fullscreen) {
					fullscreen(false);
				} else {
					fullscreen(true);
				}
			} else {
				print("Key: " # id);
			}
		}
		super:onKey(down, id);
	}

}


/**
 * Painter for the presentation window.
 */
class PresentPainter extends Painter {
	// The presentation.
	Presentation p;

	// Current slide #.
	Nat slideNr;

	// Current slide.
	Slide? slide;

	// Previous slide (for animations).
	Slide? lastSlide;

	// Current intro.
	SlideIntro? intro;

	// End of presentation brush.
	SolidBrush endColor;

	// Style.
	SlideStyle style;

	// Brush used outside the slide's limit.
	SolidBrush outsideColor;

	ctor(Presentation p) {
		init() {
			p = p;
			slideNr = 0;
			style(p);
			endColor = black;
			outsideColor = black;
		}

		if (p.slides.count > 0)
			show(p.slides[0]);
	}

	void next() {
		if (advance() == false) {
			Nat n = slideNr + 1;
			if (n >= p.slides.count) {
				show(null);
			} else {
				slideNr = n;
				show(p.slides[n]);
			}
		}
	}

	void prev() {
		if (slideNr > 0) {
			show(p.slides[--slideNr]);
		}
	}

	// Surprise key pressed.
	void surpriseKey() {
		if (slide)
			slide.onSurprise();
	}

	// Advance the current slide.
	Bool advance() {
		if (slide) {
			slide.doAnimation(true);
		} else {
			false;
		}
	}

	// Show a slide.
	void show(Slide? s) {
		cleanup;
		lastSlide = slide;
		slide = s;
		if (slide) {
			intro = slide.intro;
			slide.repaintFn = &this->repaint;
			slide.advanceFn = &this->next;

			// Start the intro first, otherwise we may repaint the intro before it has been properly
			// started due to animations in 'show'.
			if (intro) {
				intro.start;
			}
			slide.show();
		}

		repaint();
	}

	Bool render(Size s, Graphics g) {
		Size target(1024, 768);
		Bool animate = false;

		if (slide) {
			g.fill(outsideColor);

			Float scaleFactor = s.h / target.h;
			if (s.w / target.w < scaleFactor)
				scaleFactor = s.w / target.w;

			Point p = s;
			Size scaled = target * scaleFactor;
			g.transform(translate(s.center - scaled.center) * scale(scaleFactor));
			g.push();
			g.fill(Rect(Point(), target), style.bg);

			animate |= slide.animate();

			if (intro) {
				animate = true;
				Bool more = intro.draw(lastSlide, slide, style, target, g);
				if (more == false)
					this.intro = null;
			} else {
				animate |= slide.draw(style, target, g);
			}
		} else {
			g.fill(endColor);
		}

		animate;
	}

	void cleanup() {
		if (slide) {
			slide.repaintFn = null;
			slide.advanceFn = null;
		}
	}

}
