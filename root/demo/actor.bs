use core:debug;

thread Actor1;
thread Actor2;

class Output on ? {
	Str text;
	Int times;

	init(Thread, Str text, Int times) {
		init() {
			text = text;
			times = times;
		}
	}

	Int run() {
		for (Int i = 0; i < times; i++) {
			print(text * (i + 1).nat);
			sleep(150 ms);
		}
		times * 2;
	}
}

Int actorSeq() on Compiler {
	Output a(Actor1, "A", 10);
	Output b(Actor2, "B", 10);
	var x = a.run();
	print("1");
	var y = b.run();
	print("2");
	x + y;
}

Int actorSpawn() on Compiler {
	Output a(Actor1, "A", 10);
	Output b(Actor2, "B", 10);
	var x = spawn a.run();
	print("1");
	var y = spawn b.run();
	print("2");
	x.result + y.result;
}
